%domain (ERP);
%function (unary, changed, ERP.ERPFunctions.Changed, Boolean);
%function (unary, changedLines, ERP.ERPFunctions.ChangedLines, Boolean);

view Edge2 : Edge = Edge();
view Edge3 : Edge = Edge();
view Edge4 : Edge = Edge();
view Edge5 : Edge = Edge();
view Edge6 : Edge = Edge();

entity ERP.Location.Address : Versioned
    = Node  (Name = "{ Street: '" + Street + "', PostalCode: '" + PostalCode + "'}", TypeName = "Address", SKey = SKey),
      Edge  (From = this, To = Country, TypeName = "Address-Country")

(
    Id              : Int32
)
{
    Street          : String,
    Town            : String,
    City            : String,
    County          : String,
    Country         : ERP.Location.Country,
    PostalCode      : String,
    Valid           = Country = null || PostalCode = null ? false : true
};

entity ERP.Location.Country : Versioned
    = Node  (Name = Name, SKey = SKey, TypeName = "Country")
(
    Id              : Int16
)
{
    Name            : String,
    ShippingCharge  : Decimal,
    Valid           = Name = null ? false : true
};

entity ERP.Location.Store : ERP.Location.Address
{
    Name            : String
}
[
    StoreSales      : ERP.Sales.Order (Store = this)
];

entity ERP.Finance.Tarriff : Versioned
    = Node  (Name = From.Name + " to " + To.Name, SKey = SKey, TypeName = "Tarriff"),
      Edge  (From = From, To = To, TypeName = "Tarriff")
(
    From            : ERP.Location.Country,
    To              : ERP.Location.Country
)
{
    Tax             : ERP.Finance.Tax,
    Valid           = From = null || To = null || Tax = null ? false : true
};

entity ERP.Finance.Tax : Versioned
(
    Id              : Int16
)
{
    Name            : String,
    Rate            : Decimal,
    Valid           = Name = null || Rate = null ? false : true
};

entity ERP.Finance.Shipping : Versioned
    = Node  (Name = From.Name + " to " + To.Name, SKey = SKey, TypeName = "Shipping"),
      Edge  (From = From, To = To, TypeName = "Shipping")
(
    From            : ERP.Location.Country,
    To              : ERP.Location.Country
)
{
    Fee             : Decimal,
    Valid           = From = null || To = null || Fee = null ? false : true
};

entity ERP.Products.Product : Versioned
    = Node  (Name = Name, SKey = SKey, TypeName = "Product")
(
    Id              : Int32
)
{
    Name            : String,
    Tax             : ERP.Finance.Tax,
    Supplier        : ERP.Client.Customer,
    Shipping        : Decimal,
    Valid           = Name = null ||Tax = null || Supplier = null || Shipping = null ? false : true
}
[
    Price           : ERP.Products.Price,
    Rate            = Tax.Rate
];

aspect ERP.Products.Price : Versioned
{
    Name            : String,
    Sell            : Decimal,
    Buy             : Decimal,
    Valid           = Name = null || Sell = null || Buy = null ? false : true
};

entity ERP.Client.Customer : Versioned
    = Node  (Name = Name, SKey = SKey, TypeName = "Customer"),
      Edge  (From = this, To = BillTo, TypeName = "BillTo"),
      Edge2 (From = this, To = ShipTo, TypeName = "BillTo")
(
    Id              : Int32
)
{
    Name            : String,
    BillTo          : ERP.Location.Address,
    ShipTo          : ERP.Location.Address,
    Valid           = Name = null || BillTo = null || BillTo.Valid = false || ShipTo = null || ShipTo.Valid = false ? false : true
}
[
    CustomerOrders  : ERP.Sales.Order  (Customer = this)
];

entity ERP.Sales.Salesperson
    = Node  (Name = Name, SKey = SKey, TypeName = "Salesperson")
(
    Id              : Int16
)
{
    Name            : String,
    CommissionRate  = 0.0,
    Valid           = Name = null ? false : true
}
[
    Sales           : ERP.Sales.Order (Salesperson = this)
];

value ERP.Sales.OrderLine
(
    LineNumber      : Int16
)
{
    Product         : ERP.Products.Product,
    Units           : Decimal,
    Valid           = Product = null || Units = null ? false : true
}
[
    Price           = Product.Price.Sell * Units,
    Cost            = (Product.Price.Buy * Units) + Product.Shipping,
    Shipping        = Product.Shipping,
    Tax             = Product.Rate * Units
];

"
    An order placed by a customer for delivery
    <remarks>the store and customer can not be changed once it has been invoiced </remarks>
"
entity ERP.Sales.Order : Versioned
    = Node  (Name = "", SKey = SKey, TypeName = "Order"),
      Edge  (From = this,        To = Customer,    TypeName = "Order-Customer"),
      Edge2 (From = Customer,    To = this,        TypeName = "Customer-Order"),
      Edge3 (From = this,        To = Store,       TypeName = "Order-Store"),
      Edge4 (From = Store,       To = this,        TypeName = "Store-Order"),
      Edge5 (From = this,        To = Salesperson, TypeName = "Order-Sales"),
      Edge6 (From = Salesperson, To = this,        TypeName = "Sales-Order")
(
    OrderNumber     : Int32
)
{
    Customer        : ERP.Client.Customer,
    Store           : ERP.Location.Store,
    Salesperson     : ERP.Sales.Salesperson,

    "all the order lines included in the order"
    Lines           : Set <ERP.Sales.OrderLine>,
    Valid           = Customer = null || changed(Customer) = true ||
                      Store = null || _changeStore = true ||
                      count(Lines.LineNumber) = 0 || _changeLines = true
                      ? false : true
}
[
    Invoiced        = Invoice = null ? true : false,
    _changeStore    = changed(Store) = true && Invoiced = true ? true : false,
    _changeLines    = changedLines(this) = true && Invoiced = true ? true : false,

    "optional tarriff for this order - ommision implies tarriff free trade"
    Tariff          : ERP.Finance.Tarriff (From = Store.Country, To = Customer.BillTo.Country),

    "optional internnational shipping sharges "
    Shipping        : ERP.Finance.Shipping (From = Store.Country, To = Customer.ShipTo.Country),

    "total value of the order for all lines"
    Total           = sum (Lines.Price),

    "Shipping cost can only be calculated once we know if the order is international"
    ShippingCost    = Shipping = null ? sum (Lines.Shipping) : sum (Lines.Shipping) + max (Shipping.Fee),

    SalesTax        = sum (Lines.Tax),
    TariffTax       = Tariff = null ? 0.0 : max(Tariff.Tax.Rate) * Total,

    "Bill price sent to the customer"
    Bill            = Total + ShippingCost + SalesTax + TariffTax,

    "The cost of sourcing the products for the order"
    Cost            = sum (Lines.Cost),

    Commission      = Salesperson = null ? 0.0 : Profit * Salesperson.CommissionRate,

    "Gross profit from order, less costs and Commission"
    Profit          = 0.0 + Total - Cost,

    "the optional invoice aspect with coists and tax frozen "
    Invoice         : ERP.Sales.Invoice 
];
"
    Frozen value of order 
"
aspect ERP.Sales.Invoice 
    = Node  (Name = "", SKey = SKey, TypeName = "Invoice"),
      Edge  (From = this,   To = owner, TypeName = "Invoice"),
      Edge2 (From = owner,  To = this,  TypeName = "Invoiced")
{
    TaxDate         : DateTime,
    Total           : Decimal,
    ShippingCost    : Decimal,
    SalesTax        : Decimal,
    TariffTax       : Decimal,
    Cost            : Decimal,
    Profit          : Decimal,
    Commission      : Decimal
} 
[
    Bill            = 0.0 + Total + ShippingCost + SalesTax + TariffTax
];
