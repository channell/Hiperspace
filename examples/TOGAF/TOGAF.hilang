%domain(Togaf); %ids;
/*
    Togaf.Edge2 is a view that inherits its keys and attributes, and realized the view
    the implmentation allows an entity/segment/aspect to provide an additional view
    for example Principle provides an edge from part to itself and an edge from 
    itself to its parent.  
*/
view Togaf.Edge2  : Edge = Edge();
view Togaf.Edge3  : Edge = Edge();
view Togaf.Edge4  : Edge = Edge();
view Togaf.Edge5  : Edge = Edge();
view Togaf.Edge6  : Edge = Edge();
view Togaf.Edge7  : Edge = Edge();
view Togaf.Edge8  : Edge = Edge();
view Togaf.Edge9  : Edge = Edge();
view Togaf.Edge10 : Edge = Edge();

@Versioned
type Togaf.Base 
(
    Id              : Guid
)
{
    Name            : String,
    Description     : String,
    Deleted         = false
}
[
    
    ValidBase 		= Id = null || Name = null ? false : true
];

%function (unary, StrategicEdge, Togaf.TogafFunctions.StrategicEdge, Set<Graph.TransitiveEdge>);
%function (unary, Goals, Togaf.TogafFunctions.Goals, Set<Togaf.GoalRef>);
value Togaf.GoalRef #651
{
    Value           : Togaf.Business.Goal #1
};

/* ********************************************************
    General entities
******************************************************** */

segment Togaf.Has.Principle : Togaf.Base 
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Principle"),
      Edge        (From = owner, To = this, Name = Name, TypeName = "AF-Has-Principle"),
      Togaf.Edge2 (From = this, To = owner, Name = Name, TypeName = "AF-Principle-For") ;

segment Togaf.Has.Constraint : Togaf.Base  
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Constraint"),
      Edge        (From = owner, To = this, Name = Name, TypeName = "AF-Has-Constraint"),
      Togaf.Edge2 (From = this, To = owner, Name = Name, TypeName = "AF-Constraint-For") ;

segment Togaf.Has.Assumption : Togaf.Base 
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Assumption"),
      Edge        (From = owner, To = this, Name = Name, TypeName = "AF-Has-Assumption") ,
      Togaf.Edge2 (From = this, To = owner, Name = Name, TypeName = "AF-Assumption-For") ;

segment Togaf.Has.Requirement : Togaf.Base 
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Requirement"),
      Edge        (From = owner, To = this, Name = Name, TypeName = "AF-Has-Requirement") ,
      Togaf.Edge2 (From = this, To = owner, Name = Name, TypeName = "AF-Requirement-For") ;

segment Togaf.Has.Location : Togaf.Base 
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Location"),
      Edge        (From = owner, To = this, Name = Name, TypeName = "AF-Has-Location") ,
      Togaf.Edge2 (From = this, To = owner, Name = Name, TypeName = "AF-Location-For") ;

segment Togaf.Has.Gap : Togaf.Base 
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Gap"),
      Edge        (From = owner, To = this, Name = Name, TypeName = "AF-Has-Gap") ,
      Togaf.Edge2 (From = this, To = owner, Name = Name, TypeName = "AF-Gap-For");

segment Togaf.Has.WorkPackage : Togaf.Base 
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-WorkPackage"),
      Edge        (From = owner, To = this, Name = Name, TypeName = "AF-Has-WorkPackage") ,
      Togaf.Edge2 (From = this, To = owner, Name = Name, TypeName = "AF-WorkPackage-For") ,
      Graph.TransitiveEdge = StrategicEdges 
[
    "All Edges that can be projected as Transitative Edges to a Business Goal"
    @Once
    StrategicEdges  = StrategicEdge(this),
    Goals           = Goals(StrategicEdges)
];

type Togaf.General.Base : Togaf.Base, HasVectorSpace
[
    Principle       : Togaf.Has.Principle,
    Constraint      : Togaf.Has.Constraint,
    Assumption      : Togaf.Has.Assumption,
    Requirement     : Togaf.Has.Requirement,
    Location        : Togaf.Has.Location,
    Gap             : Togaf.Has.Gap,
    WorkPackage     : Togaf.Has.WorkPackage
];

/* ********************************************************
    Organization 
******************************************************** */
entity Togaf.Organization : Togaf.Base 
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Organization"),
      Edge        ( From = this, To = Parent, TypeName = "AF-Organization-Part"),
      Togaf.Edge2 ( From = Parent, To = this, TypeName = "AF-Organization-Include") #20
{
    Parent          : Togaf.Organization #5
}
[
    Children        : Togaf.Organization ( Parent = this) #12,
    Motivates       : Togaf.Business.Driver (MotivatedBy = this) #13,
    Contains        : Togaf.Business.Actor (In = this) #14,
    Functions       : Togaf.Business.Function (For = this) #15,
    Uses            : Togaf.Business.Capability ( By = this) #16,
    Produces        : Togaf.Business.Product ( By = this) #17,
    Governs         : Togaf.Service ( OwnedBy = this) #18,
    SameName        : Togaf.Organization (Name = Name) #19
];

/* ********************************************************
    Business
******************************************************** */

entity Togaf.Business.Driver : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Driver"),    
      Edge        ( From = this, To = MotivatedBy, TypeName = "AF-Driver-Organization"),
      Togaf.Edge2 ( From = MotivatedBy, To = this, TypeName = "AF-Organization-Driver") #76
{
    MotivatedBy     : Togaf.Organization #5
}
[
    Creates         : Togaf.Business.Goal ( By = this ) #75
];

entity Togaf.Business.Actor : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Actor"),
      Edge        (From = this, To = In, Name = Name, TypeName = "AF-Actor-Organization"),
      Togaf.Edge2 (From = In, To = this, Name = Name, TypeName = "AF-Organization-Actor") #88 
{
    In              : Togaf.Organization #5
}
[
    Performs        : Togaf.Has.Activity #414,
    Involved        : Togaf.Business.ValueStream (Involves = this) #87
];

entity Togaf.Business.Function : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Function"),  
      Edge        ( From = this, To = Parent, TypeName = "AF-Function-Part"),
      Togaf.Edge2 ( From = Parent, To = this, TypeName = "AF-Function-Include"),
      Togaf.Edge3 ( From = this, To = For, TypeName = "AF-Function-Organization"),
      Togaf.Edge4 ( From = For, To = this, TypeName = "AF-Organization-Function"),
      Togaf.Edge5 ( From = this, To = Delivers, TypeName = "AF-Function-Capability"),
      Togaf.Edge6 ( From = Delivers, To = this, TypeName = "AF-Capability-Function") #101
{
    For             : Togaf.Organization #5,
    Parent          : Togaf.Business.Function #6,
    Delivers        : Togaf.Business.Capability #7
}
[
    Children        : Togaf.Business.Function (Parent = this) #98,
    Orchestrates    : Togaf.Business.Process (Decomposes = this) #99,
    Involves        : Togaf.Business.CourseOfAction (Involves = this) #100
];
entity Togaf.Business.Capability : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Capability"),  
      Edge        ( From = this, To = Parent, TypeName = "AF-Capability-Part"),
      Togaf.Edge2 ( From = Parent, To = this, TypeName = "AF-Capability-Include") #116
{
    By              : Togaf.Organization #5,
    Parent          : Togaf.Business.Capability #6
}
[
    Children        : Togaf.Business.Capability (Parent = this) #111,
    Operated        : Togaf.Business.Process (Operates = this) #112,
    Delivered       : Togaf.Business.Function (Delivers = this) #113,
    EnabledBy       : Togaf.Business.ValueStream (Enables = this) #114,
    Related         : Togaf.Business.CourseOfAction (Enables = this) #115
];

entity Togaf.Business.Product : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Product"),  
      Edge        ( From = this, To = Parent, TypeName = "AF-Product-Part"),
      Togaf.Edge2 ( From = Parent, To = this, TypeName = "AF-Product-Include"),
      Togaf.Edge3 ( From = this, To = Parent, TypeName = "AF-Organisation-Product"),
      Togaf.Edge4 ( From = Parent, To = this, TypeName = "AF-Product-Organisation") #128
{
    By              : Togaf.Organization #5,
    Parent          : Togaf.Business.Product #6
}
[
    Children        : Togaf.Business.Product (Parent = this) #126,
    ProvidedBy      : Togaf.Business.Process (Provides = this) #127
];

entity Togaf.Business.Goal : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Goal"),
      Edge        ( From = this, To = By, TypeName = "AF-Goal-Driver"),
      Togaf.Edge2 ( From = By, To = this, TypeName = "AF-Driver-Goal") #140
{
    By              : Togaf.Business.Driver #5 
}
[
    RealizedBy      : Togaf.Business.Objective (For = this) #138,
    Follows         : Togaf.Business.CourseOfAction (Leads = this) #139
];

entity Togaf.Business.Objective : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Objective"),
      Edge        ( From = this, To = For, TypeName = "AF-Objective-Organisation"),
      Togaf.Edge2 ( From = For, To = this, TypeName = "AF-Organisation-Objective") #151
{
    For             : Togaf.Business.Goal #5
}
[
    Measured        : Togaf.Business.Measure ( For = this ) #150
];

entity Togaf.Business.Measure : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Measure"),
      Edge        ( From = this, To = Tracks, Name = Name, TypeName = "AF-Measure-Service"),
      Togaf.Edge2 ( From = Tracks, To = this, Name = Name, TypeName = "AF-Service-Measure"),
      Togaf.Edge3 ( From = this, To = For, Name = Name, TypeName = "AF-Measure-Objective"),
      Togaf.Edge4 ( From = For, To = this, Name = Name, TypeName = "AF-Objective-Measure") #162
{
    For             : Togaf.Business.Objective #5,
    Unit            : String #6,
    Tracks          : Togaf.Service #7
}
[
    Observation     : Togaf.History.Measure #323 
];

segment Togaf.History.Measure
    = Node        ( SKey = SKey, Name = "", TypeName = "AF-Measurement"),
      Edge        (From = owner, To = this, Name = "", TypeName = "AF-Measurement")
(
    At              : DateTime #2
)
{
    Value           : Decimal #3
};

enum Togaf.Business.ActivityType
{
    Task #1            ,
    Decision #2        ,
    Event #3           
};

entity Togaf.Business.Activity : Togaf.General.Base
    = Node         ( SKey = SKey, Name = Name, TypeName = "AF-Activity"),
      Edge         ( From = this, To = By, Name = Name, TypeName = "AF-Activity-Performed"),
      Togaf.Edge2  ( From = By, To = this, Name = Name, TypeName = "AF-Performs-Activity"), 
      Togaf.Edge3  ( From = this, To = Event, Name = Name, TypeName = "AF-Activity-Event"),
      Togaf.Edge4  ( From = Event, To = this, Name = Name, TypeName = "AF-Event-Activity"), 
      Togaf.Edge5  ( From = this, To = Uses, Name = Name, TypeName = "AF-Activity-Service"),
      Togaf.Edge6  ( From = Uses, To = this, Name = Name, TypeName = "AF-BusinesService-Activity"), 
      Togaf.Edge7  ( From = this, To = After, Name = Name, TypeName = "AF-Activity-After"),
      Togaf.Edge8  ( From = After, To = this, Name = Name, TypeName = "AF-Activity-Before"),
      Togaf.Edge9  ( From = this, To = Part, Name = Name, TypeName = "AF-Activity-Process"),
      Togaf.Edge10 ( From = Part, To = this, Name = Name, TypeName = "AF-Process-Activity") #175 
{
    By              : Togaf.Business.Actor #5,
    Type            : Togaf.Business.ActivityType #6,
    After           : Togaf.Business.Activity #7,
    Part            : Togaf.Business.Process #8,
    Event           : Togaf.Business.Event #9,
    Uses            : Togaf.Service #10
}
[
    Before          : Togaf.Business.Activity (After = this) #173,
    Originates      : Togaf.Data.Entity(Originator = this) #174
];

segment Togaf.Has.Activity : Many<Togaf.Business.Activity>
    = Edge        (From = owner, To = A, Name = A.Name, TypeName = "AF-Has-Activity"),
      Togaf.Edge2 (From = A, To = owner, Name = A.Name, TypeName = "AF-Activity-For") 
/*( 
    Item            : Togaf.Business.Activity
) */
{ 
    Comment         : String #4 
};

enum Togaf.Business.EventType
{
    Start #1           ,
    End #2             ,
    Intermediate #3    
};

entity Togaf.Business.Event : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Event"),
      Edge        ( From = this, To = Trigger, Name = Name, TypeName = "AF-Event-Service"),
      Togaf.Edge2 ( From = Trigger, To = this, Name = Name, TypeName = "AF-Service-Event") #187
{
    Type            : Togaf.Business.EventType #5,
    Trigger         : Togaf.Service #6
}
[
    Activities      : Togaf.Business.Activity ( Event = this) #186
];

entity Togaf.Business.Process : Togaf.General.Base
    = Node         ( SKey = SKey, Name = Name, TypeName = "AF-Process"),
      Edge         ( From = this, To = Decomposes, Name = Name, TypeName = "AF-Process-Function"),
      Togaf.Edge2  ( From = Decomposes, To = this, Name = Name, TypeName = "AF-Function-Process"), 
      Togaf.Edge3  ( From = this, To = Operates, Name = Name, TypeName = "AF-Process-Capability"), 
      Togaf.Edge4  ( From = Operates, To = this, Name = Name, TypeName = "AF-Capability-Process"), 
      Togaf.Edge5  ( From = this, To = Provides, Name = Name, TypeName = "AF-Process-Product"), 
      Togaf.Edge6  ( From = Provides, To = this, Name = Name, TypeName = "AF-Product-Process"), 
      Togaf.Edge7  ( From = this, To = Governed, Name = Name, TypeName = "AF-Process-Control"), 
      Togaf.Edge8  ( From = Governed, To = this, Name = Name, TypeName = "AF-Control-Process"), 
      Togaf.Edge9  ( From = this, To = Realizes, Name = Name, TypeName = "AF-Process-ValueStream"), 
      Togaf.Edge10 ( From = Realizes, To = this, Name = Name, TypeName = "AF-ValueStream-Process") #198 
{
    Decomposes      : Togaf.Business.Function #5,
    Starts          : Togaf.Business.Activity #6,
    Operates        : Togaf.Business.Capability #7,
    Provides        : Togaf.Business.Product #8,
    Governed        : Togaf.Business.Control #9,
    Realizes        : Togaf.Business.ValueStream #10
}
[
    Steps           : Togaf.Business.Activity (Part = this) #197
];

entity Togaf.Business.Control : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Control"),
      Edge        ( From = this, To = PartOf, Name = Name, TypeName = "AF-Control-Part"),
      Togaf.Edge2 ( From = PartOf, To = this, Name = Name, TypeName = "AF-Control-Includes") #210 
{
    URL             : String #5,
    PartOf          : Togaf.Business.Control #6
}
[
    Includes        : Togaf.Business.Control (PartOf = this) #208,
    Governs         : Togaf.Business.Process (Governed = this) #209
];

entity Togaf.Business.ValueStream : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-ValueStream"),
      Edge        ( From = this, To = Parent, Name = Name, TypeName = "AF-ValueStream-Part"),
      Togaf.Edge2 ( From = Parent, To = this, Name = Name, TypeName = "AF-ValueStream-Includes"), 
      Togaf.Edge3 ( From = this, To = Enables, Name = Name, TypeName = "AF-ValueStream-Capability"),
      Togaf.Edge4 ( From = Enables, To = this, Name = Name, TypeName = "AF-Capability-ValueStream"), 
      Togaf.Edge5 ( From = this, To = Involves, Name = Name, TypeName = "AF-ValueStream-Actor"),
      Togaf.Edge6 ( From = Involves, To = this, Name = Name, TypeName = "AF-Actor-ValueStream") #222 
{
    URL             : String #5,
    Parent          : Togaf.Business.ValueStream #6,
    Enables         : Togaf.Business.Capability #7,
    Involves        : Togaf.Business.Actor #8
}
[
    Children        : Togaf.Business.ValueStream (Parent = this) #220,
    RealizedBy      : Togaf.Business.Process (Realizes = this) #221
];

entity Togaf.Business.CourseOfAction : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-CourseOfAction"),
      Edge        ( From = this, To = Enables, Name = Name, TypeName = "AF-CourseOfAction-Capability"),
      Togaf.Edge2 ( From = Enables, To = this, Name = Name, TypeName = "AF-Capability-CourseOfAction"), 
      Togaf.Edge3 ( From = this, To = Leads, Name = Name, TypeName = "AF-CourseOfAction-Goal"),
      Togaf.Edge4 ( From = Leads, To = this, Name = Name, TypeName = "AF-Goal-CourseOfAction"), 
      Togaf.Edge5 ( From = this, To = Involves, Name = Name, TypeName = "AF-CourseOfAction-Function"),
      Togaf.Edge6 ( From = Involves, To = this, Name = Name, TypeName = "AF-Function-CourseOfAction") #232 
{
    URL             : String #5,
    Enables         : Togaf.Business.Capability #6,
    Leads           : Togaf.Business.Goal #7,
    Involves        : Togaf.Business.Function #8
};

/* ********************************************************
    Service
******************************************************** */

entity Togaf.Service : Togaf.General.Base 
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Service"),
      Edge        ( From = this, To = OwnedBy, Name = Name, TypeName = "AF-Service-Organization"),
      Togaf.Edge2 ( From = OwnedBy, To = this, Name = Name, TypeName = "AF-Organization-Service") #35
{
    OwnedBy         : Togaf.Organization #5
}
[
    Measures        : Togaf.Business.Measure ( Tracks = this) #30,
    Triggers        : Togaf.Business.Event ( Trigger = this) #31,
    UsedBy          : Togaf.Business.Activity ( Uses = this) #32,
    Provides        : Togaf.Data.Entity(Provider = this) #33,
    ProvidedBy      : Togaf.Application.System ( Realizes = this) #34
];

/* ********************************************************
    Data
******************************************************** */

entity Togaf.Data.Entity: Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Entity"),
      Edge        ( From = this, To = Originator, Name = Name, TypeName = "AF-Entity-Activity"),
      Togaf.Edge2 ( From = Originator, To = this, Name = Name, TypeName = "AF-Activity-Entity"),
      Togaf.Edge3 ( From = this, To = Originator, Name = Name, TypeName = "AF-Entity-Activity"),
      Togaf.Edge4 ( From = Originator, To = this, Name = Name, TypeName = "AF-Activity-Entity") #457
{
    Originator      : Togaf.Business.Activity #5,
    Provider        : Togaf.Service #6
}
[
    Logical         : Togaf.Data.Logical (Encapsulate = this) #455,
    DataUsage       : Togaf.Data.Usage (Entity = this) #456
];

entity Togaf.Data.Usage : Togaf.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-DataUsage"),
      Edge        ( From = this, To = Entity, Name = Name, TypeName = "AF-DataUsage-Entity"),
      Togaf.Edge2 ( From = Entity, To = this, Name = Name, TypeName = "AF-Entity-DataUsage"),
      Togaf.Edge3 ( From = this, To = System, Name = Name, TypeName = "AF-DataUsage-System"),
      Togaf.Edge4 ( From = System, To = this, Name = Name, TypeName = "AF-System-DataUsage") #459
(
    Entity          : Togaf.Data.Entity #2,
    System          : Togaf.Application.System #3
)
{
    Comment         : String #7
};

entity Togaf.Data.Logical : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Logical"),
      Edge        ( From = this, To = Encapsulate, Name = Name, TypeName = "AF-Logical-Entity"),
      Togaf.Edge2 ( From = Encapsulate, To = this, Name = Name, TypeName = "AF-Entity-Logical"),
      Togaf.Edge3 ( From = this, To = Component, Name = Name, TypeName = "AF-Logical-Component"),
      Togaf.Edge4 ( From = Component, To = this, Name = Name, TypeName = "AF-Component-Logical") #470
{
    Encapsulate     : Togaf.Data.Entity #5,
    Component       : Togaf.Application.Component #6
}
[
    Instantiate     : Togaf.Data.Physical (Instantiate = this) #469
];

entity Togaf.Data.Physical : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Physical"),
      Edge        ( From = this, To = Instantiate, Name = Name, TypeName = "AF-Physical-Logical"),
      Togaf.Edge2 ( From = Instantiate, To = this, Name = Name, TypeName = "AF-Logical-Physical"),
      Togaf.Edge3 ( From = this, To = Deployed, Name = Name, TypeName = "AF-Physical-Deployed"),
      Togaf.Edge4 ( From = Deployed, To = this, Name = Name, TypeName = "AF-Deployed-Physical") #480
{
    Instantiate     : Togaf.Data.Logical #5,
    Deployed        : Togaf.Application.Deployed #6
};
/* ********************************************************
    Application
******************************************************** */
entity Togaf.Application.System : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-System"),
      Edge        ( From = this, To = Realizes, Name = Name, TypeName = "AF-System-Service"),
      Togaf.Edge2 ( From = Realizes, To = this, Name = Name, TypeName = "AF-Service-System"),
      Togaf.Edge3 ( From = this, To = Platform, Name = Name, TypeName = "AF-System-Platform"),
      Togaf.Edge4 ( From = Platform, To = this, Name = Name, TypeName = "AF-Platform-System") #537
{
    Realizes        : Togaf.Service #5,
    Platform        : Togaf.Technology.Platform #6
}
[
    SystemUsage     : Togaf.Data.Usage (System = this) #535,
    ImplementedBy   : Togaf.Application.Component (Implements = this) #536
];

entity Togaf.Application.Component: Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Component"),
      Edge        ( From = this, To = Implements, Name = Name, TypeName = "AF-Component-System"),
      Togaf.Edge2 ( From = Implements, To = this, Name = Name, TypeName = "AF-System-Component"),
      Togaf.Edge3 ( From = this, To = Host, Name = Name, TypeName = "AF-Component-Host"),
      Togaf.Edge4 ( From = Host, To = this, Name = Name, TypeName = "AF-Host-Component") #549
{
    Implements      : Togaf.Application.System #5,
    Host            : Togaf.Technology.Host #6
}
[
    ComponentUsage  : Togaf.Data.Logical (Component = this) #547,
    DeployedBy      : Togaf.Application.Deployed (Deployes = this) #548
];

entity Togaf.Application.Deployed : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Deployed"),
      Edge        ( From = this, To = Deployes, Name = Name, TypeName = "AF-Deployed-Component"),
      Togaf.Edge2 ( From = Deployes, To = this, Name = Name, TypeName = "AF-Component-Deployed"),
      Togaf.Edge3 ( From = this, To = Host, Name = Name, TypeName = "AF-Deployed-Host"),
      Togaf.Edge4 ( From = Host, To = this, Name = Name, TypeName = "AF-Host-Deployed") #560
{
    Deployes        : Togaf.Application.Component #5,
    Host            : Togaf.Technology.Instance #6
}
[
    Usage           : Togaf.Data.Physical (Deployed = this) #559
];

/* ********************************************************
    Technology
******************************************************** */
entity Togaf.Technology.Platform : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Platform"),
      Edge        ( From = this, To = Serves, Name = Name, TypeName = "AF-Platform-Service"),
      Togaf.Edge2 ( From = Serves, To = this, Name = Name, TypeName = "AF-Service-Platform") #617
{
    Serves          : Togaf.Service #5
}
[
    Hosts           : Togaf.Application.System (Platform = this) #615,
    Defines         : Togaf.Technology.Host (Platform = this) #616
];

entity Togaf.Technology.Host : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Platform"),
      Edge        ( From = this, To = Platform, Name = Name, TypeName = "AF-Host-Platform"),
      Togaf.Edge2 ( From = Platform, To = this, Name = Name, TypeName = "AF-Platform-Host") #629
{
    Platform        : Togaf.Technology.Platform #5
}
[
    Hosts           : Togaf.Application.Component (Host = this) #627,
    Instances       : Togaf.Technology.Instance (Host = this) #628 
];

entity Togaf.Technology.Instance : Togaf.General.Base
    = Node        ( SKey = SKey, Name = Name, TypeName = "AF-Platform"),
      Edge        ( From = this, To = Host, Name = Name, TypeName = "AF-Instance-Platform"),
      Togaf.Edge2 ( From = Host, To = this, Name = Name, TypeName = "AF-Platform-Instance") #640
{
    Host            : Togaf.Technology.Host #5
}
[
    Hosts           : Togaf.Application.Deployed (Host = this) #639
];
