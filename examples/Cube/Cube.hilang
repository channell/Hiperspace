%domain(Cube);

"An additional edge helper for Cube.Edges"
view Cube.Edge_ : Edge = Edge();

"Bidirectional Edge, implemented with two Cube.Edges"
view Cube.Edges
    = Edge  (From = From, To = To, Name = Name, TypeName = FromTypeName) , 
      Cube.Edge_ (From = To, To = From, Name = Name, TypeName = ToTypeName)
(
    From            : Node      #7,
    To              : Node      #8,
    FromTypeName    : String    #9,
    ToTypeName      : String    #10
)
{
    Name        : String    #6,
};

view Cube.Edges2 : Cube.Edges = Cube.Edges();

@CubeDimension(Id), CubeHierarchy(Parent, Children, Tier), Versioned, DeltaIndex
entity Cube.Portfolio 
	= Node (SKey = SKey, Name = Id, TypeName = "Portfolio"),
	  Cube.Edges (From = this, To = Parent, FromTypeName = "Portfolio-Parent", ToTypeName = "Portfolio-Child", Name = Id)
(Id : String) 
{ Parent : Cube.Portfolio, Tier : Int32 } 
[Children : Cube.Portfolio (Parent = this)];

@CubeDimension(ISO)
entity Cube.Country 
	= Node (SKey = SKey, Name = ISO, TypeName = "Country")
(ISO : String) 
[Instruments : Cube.Instrument (Country = this)];

@CubeDimension(Name), CubeHierarchy(Parent, Children, Tier), Versioned, DeltaIndex
entity Cube.Sector 
	= Node (SKey = SKey, Name = Name, TypeName = "Sector") ,
	  Cube.Edges (From = this, To = Parent, FromTypeName = "Sector-Parent", ToTypeName = "Sector-Child", Name = Name) 
(Id : Int32) { Name : String, Parent : Cube.Sector, Tier : Int32 } [Children : Cube.Sector (Parent = this), Customers : Cube.Customer (Sector = this)];

@CubeDimension(Id), CubeHierarchy(Parent, Children, Tier), Versioned, DeltaIndex
entity Cube.Product 
	= Node (SKey = SKey, Name = Id, TypeName = "Product"),
	  Cube.Edges (From = this, To = Parent, FromTypeName = "Product-Parent", ToTypeName = "Product-Child", Name = Id) 
(Id : String) { Parent : Cube.Product, Tier : Int32  } 
[Children : Cube.Product (Parent = this), ProductInstruments : Cube.Instrument (Product = this), RAG = RAG (this)];

entity Cube.Account 
	= Node (SKey = SKey, Name = Id, TypeName = "Account"),
	  Cube.Edges (From = this, To = Customer, FromTypeName = "Customer-Account", ToTypeName = "Account-Customer", Name = Id)
(Id : String) {Customer : Cube.Customer} [FirstAccount : First.Acc (Account = this) ];

entity Cube.Customer 
	= Node (SKey = SKey, Name = Id, TypeName = "Customer"),
	  Cube.Edges (From = this, To = Sector, FromTypeName = "Customer-Sector", ToTypeName = "Sector-Customer", Name  = Id) 
(Id : String) {Sector : Cube.Sector} [Accounts : Cube.Account (Customer = this), FirstAccount : First.Acc ];
aspect First.Acc 
{ Account : Cube.Account };

@DeltaIndex, Versioned
aspect Cube.Price { Market : Decimal };

entity Cube.Instrument 
	= Node (SKey = SKey, Name = ISIN, TypeName = "Instrument"), 
	  Cube.Edges (From = this, To = Country, FromTypeName = "Instrument-Country", ToTypeName = "Country-Instrument", Name  = ISIN) ,
	  Cube.Edges2 (From = this, To = Product, FromTypeName = "Instrument-Product", ToTypeName = "Product-Instrument", Name  = ISIN) 
(ISIN : String) 
{Country : Cube.Country, Product : Cube.Product} [Price : Cube.Price, Market = Price.Market];

@CubeFact
entity Cube.Contract : Versioned 
	= Valued (name = "some", Value = Value, Deleted = Deleted)
	, Node (SKey = SKey, Name = "", TypeName = "Contract") 
	(Id : Int64) 
	{Quantity : Decimal, Instrument : Cube.Instrument, Portfolio : Cube.Portfolio, Account : Cube.Account} 
	[
		Market = Instrument.Market, 
		@CubeMeasure(Aggregate.Sum) 
		Value = Quantity * Market,
		@CubeMeasure(Aggregate.StdDev) 
		StdDev = Value,
		@CubeMeasure(Aggregate.Average) 
		Avg = ToDouble(Value)
	];

view Valued : Versioned
	(name : String) 
	{Value : Decimal};

view Banking.Trade (Id : String) {Book : Banking.Book};
entity Banking.FI.Trade : Banking.Trade = Banking.Trade();
entity Banking.FX.Trade : Banking.Trade = Banking.Trade();
entity Banking.EQ.Trade : Banking.Trade = Banking.Trade();
entity Banking.Book (Id : String) [Trades : Banking.Trade (Book = this)];

%function(unary, RAG, Cube.Functions.RAG, Cube.RAG);
enum Cube.RAG
{
	Red #1,
	Amber #2,
	Green #3,
	"If it does not have a parent, it does not have RAG status within it"
	None #4,
	"Missing data"
	Unknown #5
};

enum Hiperspace.TransactionState
{
    Begin #0,
    Commit #1,
    Rollback #2,
    Complete #4
};
entity Hiperspace.Transaction
(
	Id : Guid
)
{
	State : Hiperspace.TransactionState
};
