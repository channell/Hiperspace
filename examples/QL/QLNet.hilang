%domain(QL);%ids;

/*
    Functions to create QLNet objects
*/
%function(unary, makeBusinessDayConvention, QL.Helper.BusinessDayConvention, QLNet.BusinessDayConvention);
%function(unary, makeCalendar, QL.Helper.Calendar, QLNet.Calendar);
%function(unary, makeCurve, QL.Helper.FlatForward, QLNet.Handle<QLNet.YieldTermStructure>);
%function(unary, makeDayCounter, QL.Helper.DayCounter, QLNet.DayCounter);
%function(unary, makeEngine, QL.Helper.Engine, QLNet.IPricingEngine);
%function(unary, makeFixedRateBond, QL.Helper.FixedRateBond, QLNet.FixedRateBond);
%function(unary, makeInstrument, QL.Helper.Make, QLNet.Instrument);
%function(unary, makePeriod, QL.Helper.Period, QLNet.Period);
%function(unary, makeRule, QL.Helper.DateRule, QLNet.DateGeneration.Rule);
%function(unary, makeSchedule, QL.Helper.Schedule, QLNet.Schedule);
%function(unary, NPV, QL.Helper.NPV, Double);
%function(unary, CASH, QL.Helper.CASH, Double);

/*    
    Reference to QLNet types 
*/
type QLNet.ActualActual.Convention;
type QLNet.ActualActual;
type QLNet.BusinessDayConvention #30;
type QLNet.Calendar #31;
type QLNet.CashFlow;
type QLNet.DateGeneration.Rule #39;
type QLNet.DayCounter #32;
type QLNet.FixedRateBond #33;
type QLNet.Handle<QLNet.YieldTermStructure> #34;
type QLNet.IPricingEngine #35;
type QLNet.Period #36;
type QLNet.Schedule #37;
type QLNet.YieldTermStructure #38;

"values for FixedRateBond"
type QL.Values.FixedRateBond 
{
    Convention      : String,    
    Calendar        : String,
    Effective       : DateTime,
    Maturity        : DateTime,
    FaceAmount      : Double,
    SettlementDays  : Int32,
    DayCount        : String,
    Coupons         : List<Double>,
    Rate            : Double,
    Engine          : String,
};

"Calcualtion view for a fixed rate bond"
view QL.FixedRateBond : QL.Values.FixedRateBond = QL.Instrument() #58
(
    Mnmonic               : String
)
{
    NPV                   : Double,
    CASH                  : Double
};

"Base view that all Instrument types can be converted to"
view QL.Instrument #59
(
    Mnmonic               : String
)
{
    NPV                   : Double,
    CASH                  : Double
};

"A Fixed rate bond stored in hiperspace"
@CubeFact (28), CubeDimension 
entity QL.FixedRate       : QL.Values.FixedRateBond = QL.FixedRateBond() #55  /* NB Inherits properties from view, and exports for aggregation */
(
    Mnmonic		    : String #1
)
[
    _DayCount       = makeDayCounter (DayCount),
    _Calendar       = makeCalendar (Calendar),
    _ZERO           = "ZERO",
    _rule           = makeRule (_ZERO),
    _M              = "M",
    _MP             = makePeriod(_M),
    _Convention     = makeBusinessDayConvention(Convention),
    _Schedule       = makeSchedule (Effective, Maturity, _MP, _Calendar, _Convention, _rule),
    _Yield          = makeCurve (Effective, Rate, _DayCount),
    _Engine         = makeEngine (Engine, _Yield),
    @Once           /* Calculated once, and value cached for use by other functions */
    Bond            = makeFixedRateBond(SettlementDays, FaceAmount, _Schedule, Coupons, _DayCount),
    @CubeMeasure(Aggregate.Sum)
    NPV             = NPV(Bond),
    @CubeMeasure(Aggregate.Sum)
    CASH            = CASH(Bond)
];